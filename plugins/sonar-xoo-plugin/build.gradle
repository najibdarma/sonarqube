configurations {
  testCompile.extendsFrom(compileOnly)
}

dependencies {
  compile 'com.google.guava:guava'
  compile 'commons-io:commons-io'
  compile 'commons-lang:commons-lang'
  compileOnly 'com.google.code.findbugs:jsr305'
  compileOnly project(path: ':sonar-plugin-api', configuration: 'shadow')

  testCompile 'junit:junit'
  testCompile 'org.assertj:assertj-core'
  testCompile 'org.mockito:mockito-core'
}

jar {
  manifest {
    attributes(
      'Plugin-Dependencies': configurations.compile.files.collect { "META-INF/lib/$it.name" }.join(' '),
      'Plugin-Key': 'xoo',
      'Plugin-Version': project.version,
      'Plugin-Class': 'org.sonar.xoo.XooPlugin',
      'Plugin-ChildFirstClassLoader': 'false',
      'Sonar-Version': project.version,
      'SonarLint-Supported': 'true',
      'Plugin-Name': 'Xoo'
    )
  }
  into('META-INF/lib') {
      from configurations.compile
    }
}

apply plugin: 'maven-publish'
publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}
